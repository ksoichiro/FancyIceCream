plugins {
    // fabric-loom version is declared in settings.gradle
    id 'net.neoforged.moddev' version "${moddevgradle_version}" apply false
    id 'net.minecraftforge.gradle' version "${forgegradle_version}" apply false
    id 'org.parchmentmc.librarian.forgegradle' version "${parchment_version}" apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(java_version as int)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs += ['-Xlint:deprecation']
    }

    repositories {
        maven {
            name = 'Minecraft libraries'
            url = 'https://libraries.minecraft.net'
            content {
                includeModule("org.lwjgl", "lwjgl-freetype")
            }
        }
        mavenCentral()
        maven {
            name = 'Forge'
            url = 'https://maven.minecraftforge.net'
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Sponge'
                    url = 'https://repo.spongepowered.org/repository/maven-public'
                }
            }
            filter {
                includeGroupAndSubgroups('org.spongepowered')
            }
        }
    }

    afterEvaluate {
        if (!project.name.endsWith('-base') && project.name != 'common') {
            base {
                archivesName = "${mod_id}-${mod_version}+${minecraft_version}-${project.name}"
            }
        }
    }
}

apply from: 'gradle/multi-version-tasks.gradle'
