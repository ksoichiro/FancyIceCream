pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url = 'https://maven.fabricmc.net/' }
        maven { url = 'https://maven.neoforged.net/releases/' }
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }

    plugins {
        id 'fabric-loom' version settings.providers.gradleProperty('fabric_loom_version').get()
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.9.0'
}

rootProject.name = 'FancyIceCream'

def targetMcVersion = providers.gradleProperty('target_mc_version').getOrNull()
if (targetMcVersion == null) {
    throw new GradleException("'target_mc_version' property is not defined.")
}

def propsFile = file("props/${targetMcVersion}.properties")
if (!propsFile.exists()) {
    throw new GradleException("props/${targetMcVersion}.properties not found")
}
def versionProps = new Properties()
propsFile.withInputStream { versionProps.load(it) }

// Inject version-specific properties into all projects
gradle.beforeProject { project ->
    versionProps.each { key, value ->
        if (!project.hasProperty(key)) {
            project.ext.set(key, value)
        }
    }
}

def enabledPlatforms = versionProps.getProperty('enabled_platforms', '').split(',').collect { it.trim() }

// Common module (only when common/<VERSION> directory exists)
def commonDir = file("common/${targetMcVersion}")
if (commonDir.exists()) {
    include 'common'
    project(':common').projectDir = commonDir
}

// Platform modules
enabledPlatforms.each { platform ->
    def platformDir = file("${platform}/${targetMcVersion}")
    if (platformDir.exists()) {
        def baseDir = file("${platform}/base")
        // Include -base only when it exists and the version-specific module does not have its own Java sources
        def hasOwnSources = new File(platformDir, "src/main/java").exists()
        if (baseDir.exists() && !hasOwnSources) {
            include "${platform}-base"
            project(":${platform}-base").projectDir = baseDir
        }
        include platform
        project(":${platform}").projectDir = platformDir
    }
}
