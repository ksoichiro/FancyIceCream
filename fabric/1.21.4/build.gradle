plugins {
    id 'fabric-loom'
}

loom {
    accessWidenerPath = file("src/main/resources/fancyicecream.accesswidener")
}

configurations {
    commonJava {
        canBeResolved = true
    }
    commonResources {
        canBeResolved = true
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    compileOnly project(':common')
    if (findProject(':fabric-base') != null) {
        compileOnly project(':fabric-base')
    }

    commonJava project(path: ':common', configuration: 'commonJava')
    commonResources project(path: ':common', configuration: 'commonResources')
}

sourceSets.main {
    java {
        srcDir rootProject.file('common/shared/src/main/java')
        srcDir '../base/src/main/java'
    }
}

tasks.named('compileJava') {
    dependsOn configurations.commonJava
    source configurations.commonJava
}

tasks.named('processResources') {
    dependsOn configurations.commonResources
    from configurations.commonResources

    filesMatching('fabric.mod.json') {
        expand(
            version: mod_version,
            minecraft_version: minecraft_version,
            fabric_loader_version: fabric_loader_version,
        )
    }
}
