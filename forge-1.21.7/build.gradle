apply plugin: 'net.minecraftforge.gradle'
if (project.hasProperty('parchment_mc_version')) {
    apply plugin: 'org.parchmentmc.librarian.forgegradle'
    minecraft {
        mappings channel: 'parchment', version: parchment_mc_version
    }
} else {
    minecraft {
        mappings channel: 'official', version: minecraft_version
    }
}

minecraft {
    reobf = false
    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'eventbus.api.strictRuntimeChecks', 'true'
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"

    // Forge 1.21.6+ uses EventBus 7, which shifts most of its runtime validation to compile-time
    // via an annotation processor. Required for compile-time validation in dev environment.
    if ((forge_major_version as int) >= 56) {
        annotationProcessor 'net.minecraftforge:eventbus-validator:7.0-beta.10'
    }
}

// Merge resources and classes into the same directory for proper module support
sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

// Copy generated resources to the build output directory
task copyGeneratedResources(type: Copy) {
    from 'src/generated/resources'
    into sourceSets.main.output.resourcesDir
    exclude '.cache/**'
    dependsOn 'runData'
}

// Ensure data generation and copying runs before building, running, and publishing
afterEvaluate {
    tasks.matching { it.name.startsWith('run') && it.name != 'runData' }.configureEach {
        dependsOn 'copyGeneratedResources'
    }
    jar.dependsOn 'copyGeneratedResources'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_id,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_id,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${rootProject.projectDir}/mcmodsrepo"
        }
    }
}

clean {
    delete 'src/generated'
    delete 'run-data'
}
